"Start symbol" = <start>

{id_head}      = {letter} + [_]
{id_tail}      = {id_head} + {digit}
{string_ch}    = {printable} - ["]
                 
identifier     = {id_head}{id_tail}*
string         = '"' ({string_ch} | '\'{printable})* '"'
integer        = [123456789]{digit}*
real           = {Digit}*'.'{Digit}+


<start>  
    ::= <entities>
                        
<entities>    
    ::= <entities> <entity>
      | 
                        
<entity>
    ::= <entity_header> '{' <entity_body> '}'
     
<entity_header>
    ::= <entity_prefix> <identifier>

<entity_prefix>
    ::= 'device'
      | 'module'
      
<entity_body>
    ::= <members>
     
<members>
    ::= <members> <member>
      |

<member>
    ::= <member_prefix> <identifier> ';'
      | <member_prefix> <identifier> '{' <options> '}'
      
<member_prefix>
    ::= 'pin'
      | 'port'
      | 'string'
      | 'integer'
      | 'real'
      | 'net'
      | <identifier>
      
<options>
    ::= <options> <option>
      |
     
<option>
    ::= <identifier> '=' <value> ';'
                
<identifier>
    ::= identifier
 
<value>
    ::= string
      | integer
      | real
      | <identifier>
